event AppleScript_Execute:
    key:
        event_src.host, datafield12
    filter {
        filter::NotFromCorrelator()
        and event_src.title == "edr"
        and msgid == "9"
        and action == "execute"
        and object.process.fullpath == "/usr/bin/osascript"
        and regex(lower(object.process.cmdline), "(set\s+popup\s+to\s+)?display\s+dialog.+with\s+hidden\s+answer", 0) != null
    }

rule MacOS_Prompting_for_Hidden_Text_Input_via_AppleScript: AppleScript_Execute

    on AppleScript_Execute {
        $action = action
        $object = object
        $status = status

        $subject.account.name = subject.account.name
        $subject.account.fullname = subject.account.fullname
        $subject.account.domain = subject.account.domain
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges

        $subject.process.id = subject.process.id
        $subject.process.name = subject.process.name
        $subject.process.path = subject.process.path
        $subject.process.fullpath = subject.process.fullpath
        $subject.process.hash = subject.process.hash
        $subject.process.version = subject.process.version
        $subject.process.cmdline = subject.process.cmdline
        $subject.process.guid = subject.process.guid
        $subject.process.meta = subject.process.meta
        $subject.process.original_name = subject.process.original_name
        $subject.process.cwd = subject.process.cwd
        $subject.process.chain = subject.process.chain

        $subject.process.parent.id = subject.process.parent.id
        $subject.process.parent.name = subject.process.parent.name
        $subject.process.parent.path = subject.process.parent.path
        $subject.process.parent.fullpath = subject.process.parent.fullpath
        $subject.process.parent.cmdline = subject.process.parent.cmdline

        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.hash = object.process.hash
        $object.process.hash.md5 = object.process.hash.md5
        $object.process.hash.sha1 = object.process.hash.sha1
        $object.process.hash.sha256 = object.process.hash.sha256
        $object.process.version = object.process.version
        $object.process.cmdline = object.process.cmdline
        $object.process.guid = object.process.guid
        $object.process.meta = object.process.meta
        $object.process.original_name = object.process.original_name
        $object.process.cwd = object.process.cwd
        $object.process.chain = object.process.chain

        $object.process.parent.id = object.process.parent.id
        $object.process.parent.name = object.process.parent.name
        $object.process.parent.path = object.process.parent.path
        $object.process.parent.fullpath = object.process.parent.fullpath
        $object.process.parent.cmdline = object.process.parent.cmdline

        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.vendor = event_src.vendor
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule

        $datafield1 = datafield1
        $datafield2 = datafield2
        $datafield3 = datafield3
        $datafield4 = datafield4
        $datafield5 = datafield5
        $datafield6 = datafield6
        $datafield7 = datafield7
        $datafield8 = datafield8
        $datafield9 = datafield9
        $datafield10 = datafield10
        $datafield11 = datafield11
        $datafield12 = datafield12
        $datafield13 = datafield13
        $datafield14 = datafield14
        $datafield15 = datafield15
        $datafield16 = datafield16
        $datafield17 = datafield17
        $datafield18 = datafield18

        $alert.context = lower(join([subject.account.id, datafield8, datafield7], "|"))
        $alert.key = regex(lower(object.process.cmdline), "(set\s+popup\s+to\s+)?display\s+dialog.+with\s+hidden\s+answer",0)

    }

emit {
    $correlation_type = "event"

    $subject = "account"
    $action = "execute"
    $object = "process"
    $status = "success"

    $importance = "medium"

    $category.generic = "Attack"
    $category.high = "Credential Access"
    $category.low = "Input Capture: GUI Input Capture"

    $incident.aggregation.key = join([$correlation_name, lower($event_src.host), lower($subject.account.id)], "|")
    $incident.severity = $importance
    $incident.category = "SoftwareSuspiciousActivity"
    $incident.aggregation.timeout = 2h
}